
title:
 univariate intercept-only LCM-SR for depression including regressions among the residuals
 presented in the 1st full paragraph of the 2nd column on page 889
data: 
 file=currandemo.dat;
variable: 
 names=id gen trt alc1-alc5 dep1-dep5;
 usevar=dep1-dep5;
analysis: 
 estimator=ml;
 model=nocovariances; ! supresses default estimation of certain covs in model;
model:
 depint        | dep1@1 dep2@1 dep3@1 dep4@1 dep5@1; ! intercept-only in dep;    

 depres1 by dep1@1; ! create "phantom factors" to define the time-specific residuals;
 depres2 by dep2@1; ! for the SR-part of model. This is simply a programming trick so;
 depres3 by dep3@1; ! we can use the residuals as predictor and outcome variables;
 depres4 by dep4@1;
 depres5 by dep5@1;

 [dep1-dep5@0]; dep1-dep5@0; ! fixing means & vars of manfiest repeated measures to 0; 
 [depres1-depres5@0]; depres1-depres5; ! estimate variances of residuals;

 depres2-depres5 pon depres1-depres4 (1); ! estimate and equate AR parameters;

output: 
 standardized;